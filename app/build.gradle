plugins {
    id "org.sonarqube" version "2.5"
}
apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

android {
    compileSdkVersion 34
    buildToolsVersion "34.0.0"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "app.varlorg.unote"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 31
        versionName '1.9.0'

        buildConfigField "String", "applicationId", "\"app.varlorg.unote\""
    }

    namespace "app.varlorg.unote"
    testNamespace "app.varlorg.unoteTest"

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testCoverageEnabled false
        }
        debug {
            def gitBranch =  providers.exec {
                try {
                    def GITLAB_CI_COMMIT_BRANCH = System.getenv('CI_COMMIT_BRANCH')
                    if ( GITLAB_CI_COMMIT_BRANCH == null) {
                        commandLine("git", "rev-parse", "--abbrev-ref", "HEAD")
                    } else {
                        commandLine("echo", "$GITLAB_CI_COMMIT_BRANCH")
                    }
                    ignoreExitValue true
                } catch(Exception e) {
                }
                ignoreExitValue true
            }.standardOutput.asText.get().trim()

            def gitHash = providers.exec {
                try {
                    def GITLAB_CI_COMMIT_SHORT_SHA = System.getenv('CI_COMMIT_SHORT_SHA')
                    if ( GITLAB_CI_COMMIT_SHORT_SHA == null) {
                        commandLine("git", "rev-parse", "--short", "HEAD")
                    } else {
                        commandLine("echo", "$GITLAB_CI_COMMIT_SHORT_SHA")
                    }
                    ignoreExitValue true
                } catch(Exception e) {
                }
                ignoreExitValue true
            }.standardOutput.asText.get().trim()

            debuggable true
            testCoverageEnabled = true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug-git-" + gitBranch + "-" + gitHash
        }
    }

    testOptions {
        reportDir = "$project.buildDir/test/report"
    }
}

dependencies {
    implementation project(':colopicker')
    //implementation project('mylibrary')
    testImplementation 'junit:junit:4.12'
    //implementation 'com.github.yukuku:ambilwarna:2.0.1'
    //compile 'com.android.support:appcompat-v7:23.1.1'
}

android { lintOptions { disable 'MissingTranslation' } }

project.afterEvaluate {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type ->
        type.name
    }
    def productFlavors = android.productFlavors.collect { flavor ->
        flavor.name
    }

    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')

    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"

            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            task "${testTaskName}Coverage" (type:JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."

                classDirectories = fileTree(
                        dir: "${project.buildDir}/intermediates/classes/${sourcePath}",
                        excludes: [
                                '**/R.class',
                                '**/R$*.class',
                                '**/*$ViewInjector*.*',
                                '**/*$ViewBinder*.*',
                                '**/BuildConfig.*',
                                '**/Manifest*.*'
                        ]
                )

                def coverageSourceDirs = [
                        "src/main/java",
                        "src/$productFlavorName/java",
                        "src/$buildTypeName/java"
                ]
                additionalSourceDirs = files(coverageSourceDirs)
                sourceDirectories = files(coverageSourceDirs)
                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")

                reports {
                    html {
                        enabled true   
                    }
                    xml {
                        enabled true   
                    }
                }
            }
        }
    }
}

